apply plugin: 'com.android.library'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        // For developing the library outside the context of the example app, expect `react-native`
        // to be installed at `./node_modules`.
        url "$projectDir/../node_modules/react-native/android"
    }
    maven {
        // For developing the example app.
        url "$projectDir/../../react-native/android"
    }
}

def DEFAULT_COMPILE_SDK_VERSION = 28
def DEFAULT_BUILD_TOOLS_VERSION = "28.0.3"
def DEFAULT_TARGET_SDK_VERSION = 28
def DEFAULT_SUPPORT_LIB_VERSION = "28.0.0"

android {
    compileSdkVersion rootProject.hasProperty('compileSdkVersion') ? rootProject.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION
    buildToolsVersion rootProject.hasProperty('buildToolsVersion') ? rootProject.buildToolsVersion : DEFAULT_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.hasProperty('targetSdkVersion') ? rootProject.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        debug {
            buildConfigField 'boolean', 'IS_NEW_ARCHITECTURE_ENABLED', 'true'
            buildConfigField 'boolean', 'DEBUG', 'true'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'IS_NEW_ARCHITECTURE_ENABLED', 'true'
            buildConfigField 'boolean', 'DEBUG', 'false'
        }
    }
}

buildscript {
    if (project == rootProject) {
        repositories {
            mavenCentral()
        }
        dependencies {
            classpath("com.android.tools.build:gradle:7.2.2")

            // NOTE: Do not place your application dependencies here; they belong
            // in the individual module build.gradle files
        }
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

def supportVersion = rootProject.hasProperty('supportLibVersion') ? rootProject.supportLibVersion : DEFAULT_SUPPORT_LIB_VERSION

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation "com.android.support:appcompat-v7:${supportVersion}"
    implementation 'com.facebook.react:react-native:+'
}
